//Water Fountain effect, by solarsnowfall

Actor WtrFountain 20000
{
	+IsMonster
	RenderStyle Translucent
	Alpha 0
	States
	{
	Spawn:
		DROP A 1 thing_projectilegravity(1, random(200, 202), random(0, 360), random(0, 10), random(20, 60))
		Loop
	}
}

Actor DropletA
{
	SpawnId 200
	+Missile
	+DontSplash
	RenderStyle Add
	Alpha .5
	Scale .5
	States
	{
	Spawn:
		DROP A 1 
		Loop
	Death:
		DROP A 1 A_CustomMissile("DropletD", 4, 0, random(0, 360))
		DROP A 0 A_CustomMissile("DropletD", 4, 0, random(0, 360))
		DROP A 0 A_CustomMissile("DropletD", 4, 0, random(0, 360))
		Stop
	}
}

Actor DropletB : DropletA
{
	//SpawnId 201
	Scale .4	
}

Actor DropletC : DropletA 
{
	//SpawnId 202
	Scale .3
}

Actor DropletD
{
	Speed 5
	+Missile
	+DontSplash
	RenderStyle Add
	Alpha .5
	Scale .15
	States
	{
	Spawn:
		DROP A 1 
		Loop
	}
}

//Rain effect, by solarsnowfall

Actor Dripper 20002
{
	SpawnId 203
    	+Missile
	+NoClip
	+NoGravity
	+NoBlockMap
	RenderStyle Translucent
	Alpha 0
	States
	{
	Spawn:
		DROP A 1
		DROP A 1 A_CustomMissile("DripperRainDrop", 0, 0, 0)
		Stop
	}
}

Actor DripperRainDrop
{
	+Missile
	+DontSplash
	RenderStyle Add
	Alpha .5
	Xscale .5
	States
	{
	Spawn:
		DROP A 1 
		Loop
	Death:
		DROP A 1 A_SetTranslucent(.4, 1)
		DROP A 1 A_SetTranslucent(.35, 1)
		DROP A 1 A_SetTranslucent(.3, 1)
		DROP A 1 A_SetTranslucent(.25, 1)
		DROP A 1 A_SetTranslucent(.2, 1)
		DROP A 1 A_SetTranslucent(.15, 1)
		DROP A 1 A_SetTranslucent(.1, 1)
		Stop
	}
}

//Bubble Effect, by solarsnowfall

Actor BubbleA
{
	SpawnId 204
	+Missile
	RenderStyle Add
	Alpha .5
	Scale .6
	States
	{
	Spawn:
		BUBL A 2
		BUBL B 2
		BUBL C 2
		BUBL D 2
		BUBL E 2
		BUBL D 2
		BUBL C 2
		BUBL B 2
		BUBL A 2
		BUBL B 2
		BUBL C 2
		BUBL D 2
		BUBL E 2
		BUBL D 2
		BUBL C 2
		BUBL B 2
		BUBL A 2
		BUBL B 2
		BUBL C 2
		BUBL D 2
		BUBL E 2
		BUBL D 2
		BUBL C 2
		BUBL B 2
		BUBL A 1 A_SetTranslucent(.4, 1)
		BUBL A 1 A_SetTranslucent(.3, 1)
		BUBL A 1 A_SetTranslucent(.2, 1)
		BUBL A 1 A_SetTranslucent(.1, 1)
		Stop
	}
}

Actor BubbleB : BubbleA
{
	SpawnId 205
	Scale .5
}

Actor BubbleC : BubbleA
{
	SpawnId 206
	Scale .4
}

//Water splashes, by Caligari_87

ACTOR Water_Splash
{
	renderstyle add
	alpha .6
	Scale .75
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
		SPSH AA 2 A_SpawnDebris("Water_Drop")
		SPSH B 3 A_PlaySound("water/splash")
		SPSH CDEFG 3
		Stop
	}
}

ACTOR Water_Small_Splash
{
	renderstyle add
	alpha .4
	Scale .5
	+DONTSPLASH
	+NOGRAVITY
	States
	{
	Spawn:
		SPSH A 3 A_PlaySound("water/drip")
		SPSH BCDEFG 3
		Stop
	}
}

ACTOR Water_Drop
{
	renderstyle add
	alpha .5
	Health 8
	Mass 9
	Scale	.35
	projectile
	- nogravity
	States
	{
		DRIP A 70
		DRIP B 70
		DRIP C 70
		DRIP D 70
		DRIP A 70
		DRIP B 70
		DRIP C 70
		DRIP D 70
		Stop
	}
}

//
//Monsters 9600+
//

//Fish, by Phobus

ACTOR Fish 9600
{
   Health 25
   Radius 16
   Height 24
   Mass 40
   Speed 24
   PainChance 220
   MONSTER
   +NOGRAVITY
   +FLOAT
   +DONTFALL
   SeeSound "fish/alert"
   AttackSound "fish/attack"
   PainSound "fish/pain"
   DeathSound "fish/pain"
   MeleeDamage 2
   Obituary "%o is fish food"
   States
   {
   Spawn:
       FISH AB 10 A_Look
       Loop
   See:
       FISH AABBCCDD 4 A_CHASE
       Loop
   Melee:
       FISH E 4 A_FaceTarget
       FISH F 4 A_MeleeAttack
       FISH G 4
       Goto See
   Pain:
       FISH H 3
       FISH H 3 A_Pain
       Goto See
   Death:
       FISH I 5
       FISH J 10 A_Scream
       FISH K 5 A_Fall
       FISH L 5
       FISH M -1
       Stop
   Raise:
       FISH LKJI 5
       Goto See
   }
}

//Jelly, by Phobus

ACTOR Jelly 9601
{
    Health 200
    Radius 20
    Height 56
    Mass 10
    Speed 8
    PainChance 170
    MONSTER
    +NOGRAVITY
    +FLOAT
    +DONTFALL
    AttackSound "jelly/flame"
    MeleeSound "jelly/whip"
    MeleeDamage 2
    Obituary "Way to take on the Jellies..."
    HitObituary "%o was whipped by a Jelly"
    States
    {
    Spawn:
        JELL A 10 A_Look
        Loop
    See:
        JELL AABBCCDD 4 A_Chase
        Loop
    Melee:
        JELL H 4 A_FaceTarget
        JELL I 4 A_MeleeAttack
        JELL J 4
        Goto See
    Missile:
        JELL E 5 A_FaceTarget
        JELL F 5 A_FaceTarget
        JELL G 0 A_PlaySound ("jelly/flame")
        JELL G 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 0 A_Custommissile ("JELLFLAMENoDamage",36,0,0)
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 0 A_Custommissile ("JELLFLAMENoDamage",36,0,0)
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL G 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL G 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 0 A_Custommissile ("JELLFLAMENoDamage",36,0,0)
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL G 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL G 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL F 0 A_Custommissile ("JELLFLAME",36,0,0)
        JELL F 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL G 2 A_FaceTarget
        JELL F 5 A_FaceTarget
        JELL E 5 A_FaceTarget
        Goto See
    Pain:
        JELL K 6
        Goto See
    Death:
        JELL L 5
        JELL M 5
        JELL N 5 A_Fall
        JELL O 5
        JELL P -1
        Stop
    Raise:
        JELL ONML 5
        Goto See
    }
}

ACTOR JELLFLAME
{
    Radius 6
    Height 10
    Speed 5
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    States
    {
    Spawn:
        JFIR ABCDEFGH 3 BRIGHT
    Death:
        JFIR IJKLMNOP 3 BRIGHT
        Stop
    }
}


ACTOR JELLFLAMENoDamage : JELLFLAME
{
    Damage 0
}

//Nautilus, by Phobus

ACTOR Nautilus 9602
{
    Health 4000
    Radius 32
    Height 96
    Speed 16
    Mass 5000
    PainChance 5
    MeleeDamage 6
    MONSTER
    +BOSS
    +NOGRAVITY
    +FLOAT
    +DONTFALL
    +MISSILEMORE
    +MISSILEEVENMORE
    SeeSound "nautilus/see"
    MeleeSound "nautilus/melee"
    ActiveSound "nautilus/active"
    PainSound "nautilus/pain"
    DeathSound "nautilus/death"
    Obituary "%o was shocked at the power of the Nautilus"
    HitObituary "%o was pinched to death by the Nautilus"
    States
    {
    Spawn:
        LOBS AB 10 A_Look
        Loop
    See:
        LOBS A 7 A_Chase
        LOBS A 0 A_JumpIfCloser(128,29)
        LOBS B 5 A_Chase
        LOBS C 5 A_Chase
        LOBS D 3 A_Chase
        LOBS E 7 A_Chase
        Loop
    Melee:
        LOBS F 8
        LOBS G 8 A_FaceTarget
        LOBS H 5 A_MeleeAttack
        LOBS I 5 A_MeleeAttack
        LOBS G 8 A_FaceTarget
        LOBS H 5 A_MeleeAttack
        LOBS I 5 A_MeleeAttack
        LOBS G 8
        LOBS F 8 A_FaceTarget
        Goto See
    Missile:
        LOBS J 5 A_FaceTarget
        LOBS J 0 A_CustomMissile ("NautilusShield",0,0,0,0)
        LOBS J 0 A_PlaySound ("nautilus/chargeup")
        LOBS K 5 A_FaceTarget
        LOBS K 0 A_PlaySound ("nautilus/chargeup")
        LOBS L 5 A_FaceTarget
        LOBS L 0 A_PlaySound ("nautilus/chargeup")
        LOBS M 5 A_FaceTarget
        LOBS M 0 A_PlaySound ("nautilus/chargeup")
        LOBS N 5 A_CustomMissile ("NautilusBall",52,0,0,0)
        LOBS N 0 A_CustomMissile ("NautilusZap",52,0,20,0)
        LOBS N 0 A_CustomMissile ("NautilusZap",52,0,340,0)
        Goto See
        LOBS B 5 A_Chase
        LOBS C 5 A_CustomMissile ("NautilusRad",48,0,0,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,30,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,60,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,90,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,120,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,150,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,180,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,210,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,240,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,270,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,300,0)
        LOBS C 0 A_CustomMissile ("NautilusRad",48,0,330,0)
        LOBS D 3 A_Chase
        Goto See+4
    Pain:
        LOBS O 6
        LOBS O 6 A_Pain
        Goto See
    Death:
        LOBS P 10
        LOBS Q 10 A_Scream
        LOBS R 10 A_Die
        LOBS S -1
        Stop
    }
}

ACTOR NautilusBall
{
    Radius 8
    Height 16
    Speed 28
    Damage 9
    PROJECTILE
    RENDERSTYLE ADD
    Alpha 0.5
    SeeSound "nautilus/fire"
    DeathSound "nautilus/shotx"
    States
    {
    Spawn:
       LBAL A 4 Bright
       LBAL B 4 Bright A_PlaySound ("nautilus/chargeup")
       LBAL C 4 Bright
       Loop
    Death:
       LBAL DEFGH 3 Bright
       Stop
    }
}

ACTOR NautilusZap
{
    Radius 4
    Height 6
    Speed 24
    Damage 3
    PROJECTILE
    RENDERSTYLE ADD
    Alpha 0.8
    SeeSound "nautilus/chargeup"
    DeathSound "nautilus/chargeup"
    States
    {
    Spawn:
       LZAP A 5 Bright
       Loop
    Death:
       LZAP A 1 Bright
       Stop
    }
}

ACTOR NautilusRad
{
    Radius 4
    Height 8
    Speed 24
    Damage 2
    PROJECTILE
    States
    {
    Spawn:
        NULL A 5
        Goto Death
    Death:
        NULL A 2
        Stop
    }
}

ACTOR NautilusShield
{
   Radius 50
   Height 100
   Speed 0
   +NoGravity
   +NoClip
   +Shootable
   +Invulnerable
   +Reflective
   SeeSound "nautilus/fire"
   RENDERSTYLE ADD
   Alpha 0.4
   States
   {
   Spawn:
        LSHD A 14 Bright
        Goto Death
   Death:
        LSHD A 1 Bright
        Stop
   }
}

//Exploding Starfish

ACTOR FloorStarFish 9603
{
   Radius 32
   Height 16
   Speed 0
   Mass 1000000
   Health 20
   deathsound "weapons/rocklx"
   obituary "%o shouldn't shoot defenseless starfish"
   +SOLID
   +SHOOTABLE
   +NOICEDEATH
   +DONTFALL
   states
   {
   Spawn:
     STRF A 6
     loop
   Death:
     STRF A 6 bright A_Scream
     STRF A 0 A_CustomMissile ("FloorStarFishExplosion",16,0,0,0)
     stop
   }
}

ACTOR FloorStarFishExplosion
{
   Radius 8
   Height 16
   Speed 0
   PROJECTILE
   states
   {
   Spawn:
     MISL B 2 bright
     MISL B 6 bright A_Explode (random(4,16)*20,128)
     MISL C 6 bright
     MISL D 4 bright
     stop
   }
}

ACTOR CeilingStarFish : FloorStarFish 9604 
{
   +SPAWNCEILING
   +NOGRAVITY
   states
   {
   Spawn:
     STRC A 6
     loop
   Death:
     STRC A 6 bright A_Scream
     STRC A 0 A_CustomMissile ("Explosion",-2,0,0,0)
     stop
   }
}

ACTOR WallStarFish : FloorStarFish 9605 
{
   +NOGRAVITY
   Radius 8
   Height 64
   states
   {
   Spawn:
     STRW A 6
     loop
   Death:
     STRW A 6 bright A_Scream
     STRW A 0 A_CustomMissile ("Explosion",38,0,0,0)
     stop
   }
}

Actor BlackDemon 26391
{
  Health 200
  PainChance 160
  Speed 10
  Radius 30
  Height 56
  Mass 400
  Monster
  +FLOORCLIP
  SeeSound "demon/sight"
  AttackSound "demon/melee"
  PainSound "demon/pain"
  DeathSound "demon/death"
  ActiveSound "demon/active"
  Obituary "%o was bit by a Black Demon."
  States
  {
  Spawn:
    DSRG AB 10 A_Look
    Loop
  See:
    DSRG AABBCCDD 2 Fast A_Chase
    Loop
  Melee:
    DSRG EF 8 Fast A_FaceTarget
    DSRG G 8 Fast A_SargAttack
    Goto See
  Pain:
    DSRG H 2 Fast
    DSRG H 2 Fast A_Pain
    Goto See
  Death:
    DSRG I 8
    DSRG J 8 A_Scream
    DSRG K 4
    DSRG L 4 A_NoBlocking
    DSRG M 4
    DSRG N -1
    Stop
  Raise:
    DSRG N 5
    DSRG MLKJI 5
    Goto See
  }
}